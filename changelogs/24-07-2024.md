# Bite changelog 24.07.2024
- Classes can now define members
```
class Foo {
    x = 10;
    y; # default initialize to nil 
}

let foo = Foo();
print(foo.x);
print(foo.y);
```

- `this` inside of classes is now implicit 
```
class Foo {
    x;
    
    get_x() {
        return x; # this is implicit
    }
    
    display() {
        print(get_x()); # also with methods
    }
    
    init(x) {
        # note that method parameters and local variables
        # have higher precedence than members with same name
        this.x = x;
    }
}
```

- Class methods are now hoisted inside of classes
```
class Foo {
    display() {    
        let x = get_10(); # previosuly compile time error
        print(x);
    }
    
    get_10() {
        return 10;
    }
}
```

- Class members can now be private
```
class Foo {
    private x = 10;
    private get_x() {
        return x;
    }
    
    display_x() {
        print(get_x()); # private members can be accessed in class
    }
}

let foo = Foo();
print(foo.x); # runtime error!
```

- Class members can now be static
```
class Foo {
    private static count = 0; # static members can also be private
    static get_count() {
        return count;
    }
    
    display() {
        print(get_count()); # implicit static call 
    }
}

print(Foo.get_count());
let instance = Foo();
instance.get_count(); # runtime error! static members cannot be accessed on instances
```
- Class constructors now can call super class constructors
```
class Foo {
    x;
    init(x) {
        this.x = x;
    }
}

class Bar : Foo {
    y;
    # note that if superclass defines constructor with 
    # non-empty parameter list then super constructor call is required.
    init(x, y) : super(x) {
        this.x = x;
    }
}
```

- Class overrides are now explicit
```
class Foo {
    x = 10;
    get_name() {
        return foo;
    }
}

class Bar : Foo {
    # compile time error if override keyword is omitted
    override x = 20;
    
    override get_name() {
        return "bar";
    }
    
    # compile time error if function with `override`
    # keyword does not actually override anything
    override add(a, b) {
        ...
    }
}
```

- Class can now be abstract and contain abstract methods and fields
```
abstract class Foo {
    # note that abstract memebers allowed only in abstract classes
    abstract bar;
    abstract foo();
    
    baz() {
        return bar + foo();
    }
}

class Bar : Foo {
    override bar = 5;
    override foo() {
        return 10;
    }
}
let instance = Bar();
print(instance.baz()); # prints 15

let error = Foo(); # runtime error! abstract classes cannot be initialized
```